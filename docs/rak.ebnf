
chunk             ::= stmt* EOF

stmt              ::= block
                    | let_decl
                    | assign_stmt
                    | if_stmt
                    | while_stmt
                    | do_while_stmt
                    | break_stmt
                    | continue_stmt
                    | echo_stmt
                    | expr_stmt

block             ::= "{" stmt* "}"

let_decl          ::= "let" IDENT ( "=" expr )? ";"

assign_stmt       ::= "&" IDENT "=" expr ";"

if_stmt           ::= "if" let_decl? expr block if_stmt_cont

if_stmt_cont      ::= ( "else" ( if_stmt | block ) )?

while_stmt        ::= "while" expr block

do_while_stmt     ::= "do" block "while" expr ";"

break_stmt        ::= "break" ";"

continue_stmt     ::= "continue" ";"

echo_stmt         ::= "echo" expr ";"

expr_stmt         ::= expr ";"

expr              ::= and_expr or_expr_cont

or_expr_cont      ::= ( "||" and_expr or_expr_cont )?

and_expr          ::= eq_expr and_expr_cont

and_expr_cont     ::= ( "&&" eq_expr and_expr_cont )?

eq_expr           ::= cmp_expr ( ( "==" | "!=" ) cmp_expr )*

cmp_expr          ::= range_expr ( ( ">=" | ">" | "<=" | "<" ) range_expr )*

range_expr        ::= add_expr ( ".." add_expr )?

add_expr          ::= mul_expr ( ( "+" | "-" ) mul_expr )*

mul_expr          ::= unary_expr ( ( "*" | "/" | "%" ) unary_expr )*

unary_expr        ::= ( "!" | "-" )? unary_expr | subscr_expr

subscr_expr       ::= prim_expr ( "[" expr "]" | "." IDENT )*

prim_expr         ::= "nil" | "false" | "true" | NUMBER | STRING | IDENT
                    | array
                    | record
                    | if_expr
                    | group

array             ::= "[" ( expr ( "," expr )* )? "]"

record            ::= "{" ( field ( "," field )* )? "}"

field             ::= IDENT ":" expr

if_expr           ::= "if" expr block_expr if_expr_cont

block_expr        ::= "{" expr "}"

if_expr_cont      ::= ( "else" ( if_expr | block_expr ) )?

group             ::= "(" expr ")"
